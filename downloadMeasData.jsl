// Use local namespace
Names Default To Here(1);

// Directory on the website to extract data from
datdir="http://wil4dyld1.mpd.analog.com/WAT/DEVELOPMENT_WAT/";

// Downloads the measurement from the website and saves it inside the saved_data folder.
//
// dataname - indicates the name of the data file to get.  ie if dataname is SAM, then
//   the data should be in 
//     http://wil4dyld1.mpd.analog.com/WAT/DEVELOPMENT_WAT/SAM.txt.zip
//   and the limits should be in
//     http://wil4dyld1.mpd.analog.com/WAT/DEVELOPMENT_WAT/SAM.LIMITS.txt
//   and the WAT key table is
//     http://wil4dyld1.mpd.analog.com/WAT/DEVELOPMENT_WAT/DEVELOPMENT_WAT_KEY_TABLE.txt
// getlimits - boolean indicating whether to actually download the limits file.
//
// The results will be saved as saved_data/SAM.jmp, saved_data/SAM.LIMITS.jmp,
// and saved_data/WAT_Key.jmp.
//
// Adds columns to the limits file for AXIS_LOW, AXIS_HIGH, AXIS_INC, AXIS_SCALE, and
// VALUE_INVERT, then applies daveGroundRules.jsl to the limits before saving.
downloadMeasData=Function( {dataname, getlimits, ground rules key},{Default Local},

	// Create saved_data if it doesn't already exist
	if (not(directory exists(::scriptdir||"saved_data")),
		create directory(::scriptdir||"saved_data"););

	// Get the zip data from the website, import it, and save it.
	Write("Getting main data...\!N");
	zipdt = open(datdir||dataname||".txt.zip",Zip);
	zip_member_list = zipdt << dir;
	zip_file = zipdt << read(zip_member_list[1], format(blob));
	dtRaw=Open(
		zip_file,
		columns(
			Column( "WAT_FILE_KEY", Numeric, Continuous, Format( "Best", 10 ) ),
			Column( "PARAMETER", Character, Nominal ),
			Column( "UNITS", Character, Nominal ),
			Column( "WAFER", Nominal, Format( "Best", 10 ) ),
			Column( "SITE", Numeric, Continuous, Format( "Best", 10 ) ),
			Column( "XCOORD", Numeric, Continuous, Format( "Best", 10 ) ),
			Column( "YCOORD", Numeric, Continuous, Format( "Best", 10 ) ),
			Column( "VALUE", Numeric, Continuous, Format( "Best", 10 ) ),
			Column( "LOW_LIM", Numeric, Continuous, Format( "Best", 10 ) ),
			Column( "HIGH_LIM", Numeric, Continuous, Format( "Best", 10 ) ),
			Column( "LOW_VALID", Numeric, Continuous, Format( "Best", 10 ) ),
			Column( "HIGH_VALID", Numeric, Continuous, Format( "Best", 10 ) )
		),
		Import Settings(
			End Of Line( CRLF, CR, LF ),
			End Of Field( Tab, Comma, CSV( 1 ) ),
			Strip Quotes( 1 ),
			Use Apostrophe as Quotation Mark( 0 ),
			Scan Whole File( 1 ),
			Treat empty columns as numeric( 0 ),
			CompressNumericColumns( 0 ),
			CompressCharacterColumns( 0 ),
			CompressAllowListCheck( 0 ),
			Labels( 1 ),
			Column Names Start( 1 ),
			Data Starts( 2 ),
			Lines To Read( "All" ),
			Year Rule( "20xx" )
		)
	);
	close(dtRaw,Save(::scriptdir||"saved_data/"||dataname||".jmp"));
	
	// Load the limit data from the website, sort it by parameter, add necessary spec columns,
	// apply Dave's rules, and save it.
	If( getlimits,
		Write("Getting limit data...\!N");
		dtLimits=Open(
			datdir||dataname||".LIMITS.txt",
			columns(
				Column( "PARAMETER", Character, Nominal ),
				Column( "UNITS", Character, Nominal ),
				Column( "SPEC_LOW", Numeric, Continuous, Format( "Best", 10 ) ),
				Column( "SPEC_HIGH", Numeric, Continuous, Format( "Best", 10 ) ),
				Column( "VALID_LOW", Numeric, Continuous, Format( "Best", 10 ) ),
				Column( "VALID_HIGH", Numeric, Continuous, Format( "Best", 10 ) )
			),
			Import Settings(
				End Of Line( CRLF, CR, LF ),
				End Of Field( Tab, Comma, CSV( 1 ) ),
				Strip Quotes( 1 ),
				Use Apostrophe as Quotation Mark( 0 ),
				Scan Whole File( 1 ),
				Treat empty columns as numeric( 0 ),
				CompressNumericColumns( 0 ),
				CompressCharacterColumns( 0 ),
				CompressAllowListCheck( 0 ),
				Labels( 1 ),
				Column Names Start( 1 ),
				Data Starts( 2 ),
				Lines To Read( "All" ),
				Year Rule( "20xx" )
			)
		);
		dtLimits << Sort(By(:PARAMETER),Order(Ascending),Replace Table);
		dtLimits << New Column( "AXIS_LOW", Numeric, Continuous, Format("Best",10));
		dtLimits << New Column( "AXIS_HIGH", Numeric, Continuous, Format("Best",10));
		dtLimits << New Column( "AXIS_INC", Numeric, Continuous, Format("Best",10));
		dtLimits << New Column( "AXIS_SCALE", Character, Nominal, <<Set Each Value("linear"));
		dtLimits << New Column( "VALUE_INVERT", Numeric, Continuous, Format("Best",10));
		dtLimits << New Column( "MINOR_TICKS", Numeric, Continuous, Format("Best",10), Set Each Value(0));
		dtLimits << New Column( "FORMAT", Character,Nominal, <<Set Each Value("Format(\!"Best\!",10)"));
		groundrules=include(::scriptdir||"daveGroundRules.jsl",<<ParseOnly);
		eval(groundrules);
		Close(dtLimits,Save(::scriptdir||"saved_data/"||dataname||".LIMITS.jmp"));
	);
	
	
	// Load the WAT Key table and save it.
	Write("Getting WAT key data...\!N");
	keyname="DEVELOPMENT_WAT_KEY_TABLE.txt";
	Open(
		datdir||keyname,
		columns(
			Column( "WAT_FILE_KEY", Numeric, Continuous, Format( "Best", 10 ) ),
			Column( "FILE", Character, Nominal ),
			Column( "INT_FILE_NAME", Character, Nominal ),
			Column( "LOT_ID", Character, Nominal ),
			Column( "TYPE_NO", Character, Nominal ),
			Column( "PROCESS", Character, Nominal ),
			Column( "PCM_SPEC", Character, Nominal ),
			Column( "DATE", Character, Nominal ),
			Column( "EQUIP", Character, Nominal ),
			Column(
				"INSERT_TIMESTAMP",
				Numeric,
				Continuous,
				Format( "y/m/d h:m:s", 22, 0 ),
				Input Format( "y/m/d h:m:s", 0 )
			)
		),
		Import Settings(
			End Of Line( CRLF, CR, LF ),
			End Of Field( Tab, Comma, CSV( 1 ) ),
			Strip Quotes( 1 ),
			Use Apostrophe as Quotation Mark( 0 ),
			Scan Whole File( 1 ),
			Treat empty columns as numeric( 0 ),
			CompressNumericColumns( 0 ),
			CompressCharacterColumns( 0 ),
			CompressAllowListCheck( 0 ),
			Labels( 1 ),
			Column Names Start( 1 ),
			Data Starts( 2 ),
			Lines To Read( "All" ),
			Year Rule( "20xx" )
		)
	);
	Close(keyname,Save(::scriptdir||"saved_data/WAT_Key.jmp"));
	Write("Done fetching data.\!N");
);